# Delete entries from $HISTFILE using fzf, works like ^R history widget
# Got this gem from https://github.com/junegunn/fzf/issues/3522#issuecomment-1872415948
hist_delete_fzf() {
  local +h HISTORY_IGNORE=
  local -a ignore
  fc -pa "$HISTFILE"
  selection=$(fc -rl 1 |
  		awk '{ cmd=$0; sub(/^[ \t]*[0-9]+\**[ \t]+/, "", cmd); if (!seen[cmd]++)  print $0}' |
  		fzf --multi --bind 'enter:become:echo {+f1}')
  if [ -n "$selection" ]; then
  	while IFS= read -r line; do ignore+=("${(b)history[$line]}"); done < "$selection"
  	HISTORY_IGNORE="(${(j:|:)ignore})"
  	# Write history excluding lines that match `$HISTORY_IGNORE` and read new history.
  	fc -W && fc -p "$HISTFILE"
  else
  	echo "nothing deleted from history"
  fi
}
zle -N hist_delete_fzf

fg_widget() {
  fg
}

zle -N fg_widget

# Yazi shell wrapper to change dirs fast
function y() {
  local tmp="$(mktemp -t "yazi-cwd.XXXXXX")" cwd
  yazi "$@" --cwd-file="$tmp"
  if cwd="$(command cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
    builtin cd -- "$cwd"
  fi
  rm -f -- "$tmp"
}

# Fix for autosuggestions breaking when selecting a suggestion
# more info: https://github.com/zsh-users/zsh-autosuggestions/issues/816
menu-select-wrap() {
  zle menu-select
}
zle -N menu-select-wrap

menu-complete-wrap() {
  zle menu-complete
}
zle -N menu-complete-wrap

change_terminal_colorscheme() {
  zsh-sync-colors.sh
}
zle -N change_terminal_colorscheme

function cdi_widget() {
  zle -I
  cdi
  zle reset-prompt
}

zle -N cdi_widget

# Regular function with the logic
tmux-dotfiles() {
  local session_name="dotfiles"
  local session_path="$HOME/dotfiles"

  if [[ -n "$TMUX" ]]; then
    local current_session=$(tmux display-message -p '#S')
    if [[ "$current_session" == "$session_name" ]]; then
      tmux detach-client
    else
      if tmux has-session -t "$session_name" 2>/dev/null; then
        tmux switch-client -t "$session_name"
      else
        tmux new-session -d -s "$session_name" -c "$session_path"
        tmux switch-client -t "$session_name"
      fi
    fi
  else
    if tmux has-session -t "$session_name" 2>/dev/null; then
      tmux attach-session -t "$session_name"
    else
      tmux new-session -s "$session_name" -c "$session_path"
    fi
  fi
}

# Widget that calls the function
function tmux_dotfiles_widget() {
  zle push-line
  BUFFER="tmux-dotfiles"
  zle accept-line
}
zle -N tmux_dotfiles_widget

# Create a directory and cd into it
mkcd() { mkdir -p "$1" && cd "$1"; }

# open sesh outside tmux
function sesh-sessions() {
  {
    exec </dev/tty
    exec <&1
    local session
    session=$(sesh list -i -d | fzf --height 40% --reverse --border-label ' sesh ' --border --ansi --prompt 'âš¡  ')
    zle reset-prompt > /dev/null 2>&1 || true
    [[ -z "$session" ]] && return
    sesh connect $session
  }
}

zle -N sesh-sessions
